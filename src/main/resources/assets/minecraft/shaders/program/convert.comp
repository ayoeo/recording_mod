#version 430

layout(local_size_x = 32, local_size_y = 32) in;

layout(binding = 0, rgba8) uniform readonly image2D input_image;

layout(binding = 1) writeonly buffer Y { uint y_ch[]; };

layout(binding = 2) writeonly buffer U { uint u_ch[]; };

layout(binding = 3) writeonly buffer V { uint v_ch[]; };

vec3 color_at(uint x, uint y) {
  return imageLoad(input_image, ivec2(x, y)).rgb;
}

ivec3 yuv_from_rgb(vec3 rgb) {
  float r = rgb.r * 255;
  float g = rgb.g * 255;
  float b = rgb.b * 255;
  float y = 0.299 * r + 0.587 * g + 0.114 * b;
  float u = -0.169 * r - 0.331 * g + 0.500 * b + 128;
  float v = 0.500 * r - 0.419 * g - 0.081 * b + 128;
  return ivec3(y, u, v);
}

uint pack_4(ivec4 bytes) {
  return bytes.x | (bytes.y << 8) | (bytes.z << 16) | (bytes.w << 24);
}

void main() {
  ivec2 size = imageSize(input_image);
  uint width = size.x / 4;

  uint x = gl_GlobalInvocationID.x;
  uint y = gl_GlobalInvocationID.y;

  if (x >= width || y >= size.y) {
    return;
  }

  uint inv_y = size.y - y;

  ivec3 p0 = yuv_from_rgb(color_at(x * 4 + 0, inv_y));
  ivec3 p1 = yuv_from_rgb(color_at(x * 4 + 1, inv_y));
  ivec3 p2 = yuv_from_rgb(color_at(x * 4 + 2, inv_y));
  ivec3 p3 = yuv_from_rgb(color_at(x * 4 + 3, inv_y));

  y_ch[x + y * width] = pack_4(ivec4(p0.x, p1.x, p2.x, p3.x));
  u_ch[x + y * width] = pack_4(ivec4(p0.y, p1.y, p2.y, p3.y));
  v_ch[x + y * width] = pack_4(ivec4(p0.z, p1.z, p2.z, p3.z));
}
